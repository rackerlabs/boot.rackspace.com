#!ipxe

goto ${menu} ||

:linux_menu
menu Linux Installers - Current Arch [ ${arch} ]
iseq ${arch} x86_64 && set arch_a amd64 || set arch_a ${arch}
item --gap Popular Linux Operating Systems:
item archlinux ${space} Arch Linux
item centos ${space} CentOS 
item debian ${space} Debian
item fedora ${space} Fedora
item kali ${space} Kali Linux
item mageia ${space} Mageia
item opensuse ${space} openSUSE
item ubuntu ${space} Ubuntu
item --gap All Others:
item antergos ${space} Antergos
item architect ${space} Architect Linux
item coreos ${space} CoreOS
item rancheros ${space} RancherOS
item scientific ${space} Scientific
item slackware ${space} Slackware
item tinycore ${space} TinyCoreLinux
# Options
item --gap Options:
iseq ${arch} x86_64 && set bits 64 || set bits 32
item changebits ${space} Architecture: ${arch} (${bits}bit)
choose menu || goto linux_exit
echo ${cls}
goto ${menu} ||
iseq ${sigs_enabled} true && goto verify_sigs || goto change_menu

:verify_sigs
imgverify ${menu}.ipxe ${sigs}${menu}.ipxe.sig || goto error
goto change_menu

:change_menu
chain ${menu}.ipxe || goto error
goto linux_menu

:linux_exit
clear menu
exit 0

:changebits
iseq ${arch} x86_64 && set arch i386 || set arch x86_64
goto linux_menu
