#!ipxe

# Debian Operating System
# http://www.debian.org

goto ${menu}

:debian
set os Debian

menu ${os} - ${arch_a}
item jessie ${os} 8.0 (jessie)
item wheezy ${os} 7.0 (wheezy)
item squeeze ${os} 6.0 (squeeze)
item stretch ${os} stretch (testing)
item sid ${os} sid (unstable) 
choose version || goto debian_exit
set mirrorcfg mirror/suite=${version} mirror/country=manual mirror/http/hostname=${mirror} mirror/http/directory=/debian

:deb_boot_type
set dir debian/dists/${version}/main/installer-${arch_a}/current/images/netboot/

menu ${os} boot parameters
item text ${os} text based install
item graphical ${os} graphical based install
item rescue ${os} rescue mode 
item expert ${os} expert install
item preseed ${os} specify preseed url
choose --default ${type} type || goto debian

echo ${cls}
goto deb_${type}

:deb_rescue
set install_params rescue/enable=true
goto deb_text

:deb_expert
set install_params priority=low
goto deb_text

:deb_preseed
echo -n Specify preseed URL for ${os} ${version}: && read preseedurl
set install_params auto=true priority=critical preseed/url=${preseedurl}
goto deb_text

:deb_text
set dir ${dir}${menu}-installer/${arch_a}
goto deb_boot

:deb_graphical
set dir ${dir}gtk/${menu}-installer/${arch_a}
set install_params vga=788
goto deb_boot

:deb_boot
imgfree
echo Boot parameters: ${install_params} -- quiet ${params}
kernel http://${mirror}/${dir}/linux ${install_params} ${netcfg} ${mirrorcfg} -- quiet ${params} initrd=initrd.gz
initrd http://${mirror}/${dir}/initrd.gz
boot

:debian_exit
clear menu
exit 0
