#!ipxe

# Fedora Operating System
# http://wwww.fedora.org

goto ${menu} ||

:fedora
clear osversion
clear sku_type
clear ova
set os Fedora
menu Fedora ${arch}
item 23 ${os} 23
item 22 ${os} 22
item 21 ${os} 21
item 20 ${os} 20
isset ${osversion} || choose osversion || goto linux_menu
set ova ${os} ${osversion}
iseq ${osversion} 20 && goto legacy_sku ||
iseq ${osversion} 19 && goto legacy_sku ||
iseq ${osversion} 18 && goto legacy_sku ||
goto product_sku

:product_sku
menu ${os} ${arch} sku type
item Server ${ova} Server
item Workstation ${ova} Workstation
item Cloud_Atomic ${ova} Cloud Atomic
isset ${sku_type} || choose sku_type || goto fedora
set dir fedora/releases/${osversion}/${sku_type}/${arch}/os
set ova ${ova} ${sku_type}
echo ${cls}
goto boottype

:legacy_sku
set dir fedora/releases/${osversion}/${os}/${arch}/os
goto boottype

:boottype
menu ${os} ${arch} boot type
item normal ${ova} install
item rescue ${ova} rescue
item kickstart ${ova} specify kickstart url
isset ${bt} || choose bt || goto fedora
echo ${cls}
iseq ${bt} rescue && goto rescue ||
iseq ${bt} kickstart && goto kickstart ||
goto boot

:rescue
set params rescue ||
goto boot

:kickstart
echo -n Specify kickstart URL for ${os} ${osversion}: && read ksurl
set params ks=${ksurl} ||
goto boot

:boot
imgfree
kernel http://${mirror}/${dir}/images/pxeboot/vmlinuz repo=http://${mirror}/${dir} ${params} ${netcfg} || goto change_mirror
initrd http://${mirror}/${dir}/images/pxeboot/initrd.img ||
boot || 
goto linux_menu

:change_mirror
chain --autofree mirrors.ipxe
goto boot

:linux_menu
clear menu
exit 0
